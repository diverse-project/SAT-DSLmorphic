grammar org.xtext.example.mydsl1.MSat with org.eclipse.xtext.common.Terminals


import "http://www.eclipse.org/emf/2002/Ecore" as ecore 

generate mSat "http://www.xtext.org/example/mydsl1/MSat"

SATMorphic:
	'solver' solvers+=SATSolver+ 
	benchmark=Benchmark
;


SATSolver : solver=(Sat4J | CryptoMiniSAT | MiniSAT) (version=SolverVersion)?;

Sat4J:  variant=Sat4JVariant;
enum Sat4JVariant:	SAT4J_JAVA="sat4j-java" | SAT4J_JAR="sat4j-jar" | SAT4J_COMP="sat4j-maven" ;

SolverVersion: "version" version=STRING;

MiniSAT: variant="minisat" parameter=MiniSATParameter?;
CryptoMiniSAT: variant="cryptominisat" ;

// https://www.msoos.org/minisat-faq/
MiniSATParameter : 'rnd-freq' rndfreq=PROBA; // STRING; 

terminal PROBA returns ecore::EFloat : '1'|('0'(.INT)?);

Benchmark: BenchmarkDimacs | BenchmarkFormula;

BenchmarkDimacs : 'benchmarkDIMACS' (dimacses+=STRING ("," dimacses+=STRING)*);
BenchmarkFormula : 'benchmarkFormula'	(expressions+=BiImpl ("," expressions+=BiImpl)*); 

BiImpl returns Expression:
	Impl ( {BiImpl.left=current} '<=>' right=Impl)*
;

Impl returns Expression:
	Or ({Impl.left=current} '=>' right=Or)*
;

Or returns Expression:
	And ({Or.left=current} 'v' right=And)*
;

And returns Expression:
	Nand ({And.left=current} '^' right=Nand)*	
;

Nand returns Expression:
	Primary ({Nand.left=current} ('|'|'â†‘') right=Primary)*
;

Primary returns Expression:
	'(' BiImpl ')' |
	Not |
	Const |
	Var 	
;

Not returns Expression:
	('!'|'~') Primary {Not.expression=current}
;

Var returns Expression:
	id=ID
;

Const returns Expression:
	val=("true"|"false")
;



