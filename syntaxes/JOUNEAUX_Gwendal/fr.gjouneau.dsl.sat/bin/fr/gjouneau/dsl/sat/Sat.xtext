grammar fr.gjouneau.dsl.sat.Sat with org.eclipse.xtext.common.Terminals

generate sat "http://www.gjouneau.fr/dsl/sat/Sat"

ModelSAT:
	{ModelSAT} (expression+=Expression(";" expression+=Expression)*)?;

Expression: Equiv;

Equiv returns Expression:
	Implies (
		{Equiv.lhs=current} ("EQUIV"|"<=>") rhs=Implies
	)*;

Implies returns Expression:
	Or (
		{Implies.lhs=current} ("IMPLIES"|"->") rhs=Or
	)*;
	
Or returns Expression:
	And (
		{Or.lhs=current} ("OR"|"||") rhs=And
	)*;

And returns Expression:
	Nand (
		{And.lhs=current} ("AND"|"&&") rhs=Nand
	)*;
	
Nand returns Expression:
	Primary (
		{Nand.lhs=current} ("NAND"|"!&") rhs=Primary
	)*;
	
Primary returns Expression:
	'(' Expression ')' |
	{Not} ("!"|"NOT") expression=Primary |
	Atomic
;

Atomic returns Expression:
	{Variable} name=ID
;