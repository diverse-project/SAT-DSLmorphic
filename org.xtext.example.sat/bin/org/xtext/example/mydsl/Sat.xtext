grammar org.xtext.example.mydsl.Sat with org.eclipse.xtext.common.Terminals

generate sat "http://www.xtext.org/example/mydsl/Sat"



Formula returns Formula:
	Model | Instruction
;

Instruction returns Instruction:
	(solver = Solver)? (("sat" expr=Model) | ("load" path=STRING))
;

Solver returns Solver:
	str=("sat4j-java" | "sat4j-jar" | "sat4j-maven" )
;

Model returns Expression:
	BiImpl
;

BiImpl returns Expression:
	Impl ( {BiImpl.left=current} '<=>' right=Impl)*
;

Impl returns Expression:
	Or ({Impl.left=current} '=>' right=Or)*
;

Or returns Expression:
	And ({Or.left=current} 'v' right=And)*
;

And returns Expression:
	Nand ({And.left=current} '^' right=Nand)*	
;

Nand returns Expression:
	Primary ({Nand.left=current} ('|'|'â†‘') right=Primary)*
;

Primary returns Expression:
	'(' Model ')' |
	Not |
	Const |
	Var 	
;

Not returns Expression:
	('!'|'~') Primary {Not.expression=current}
;

Var returns Expression:
	id=ID
;

Const returns Expression:
	val=("true"|"false")
;


