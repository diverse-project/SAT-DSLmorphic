grammar org.xtext.example.mydsl.Sat with org.eclipse.xtext.common.Terminals

generate sat "http://www.xtext.org/example/mydsl/Sat"

SAT : sat=(File | Model) callMethod=SATCallMethod;

enum SATCallMethod: 
	SAT4J_JAVA='sat4j-java' 
	| SAT4J_JAR='sat4J-jar'
	| SAT4J_MAVEN='sat4j-maven';

File : 'file:' dimacsFileName=STRING;

Model returns Expression:
	BiImpl
;

BiImpl returns Expression:
	Impl ( {BiImpl.left=current} '<=>' right=Impl)*
;

Impl returns Expression:
	Or ({Impl.left=current} '=>' right=Or)*
;

Or returns Expression:
	And ({Or.left=current} 'v' right=And)*
;

And returns Expression:
	Nand ({And.left=current} '^' right=Nand)*	
;

Nand returns Expression:
	Primary ({Nand.left=current} ('|'|'â†‘') right=Primary)*
;

Primary returns Expression:
	'(' Model ')' |
	Not |
	Const |
	Var 	
;

Not returns Expression:
	('!'|'~') Primary {Not.expression=current}
;

Var returns Expression:
	id=ID
;

Const returns Expression:
	val=("true"|"false")
;


