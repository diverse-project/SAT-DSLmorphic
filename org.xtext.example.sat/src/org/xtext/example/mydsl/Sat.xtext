grammar org.xtext.example.mydsl.Sat with org.eclipse.xtext.common.Terminals

generate sat "http://www.xtext.org/example/mydsl/Sat"

Model:
	'solver' solver=Solver 
	(expression=BiImpl | file=File)
;

enum Solver:
	SAT4J_JAVA="sat4j-java" | SAT4J_JAR="sat4j-jar" | SAT4J_COMP="sat4j-maven"
;

File:
	'file' path=STRING
;

BiImpl returns Expression:
	Impl ( {BiImpl.left=current} '<=>' right=Impl)*
;

Impl returns Expression:
	Or ({Impl.left=current} '=>' right=Or)*
;

Or returns Expression:
	And ({Or.left=current} 'v' right=And)*
;

And returns Expression:
	Nand ({And.left=current} '^' right=Nand)*	
;

Nand returns Expression:
	Primary ({Nand.left=current} ('|'|'â†‘') right=Primary)*
;

Primary returns Expression:
	'(' BiImpl ')' |
	Not |
	Const |
	Var 	
;

Not returns Expression:
	('!'|'~') Primary {Not.expression=current}
;

Var returns Expression:
	id=ID
;

Const returns Expression:
	val=("true"|"false")
;


